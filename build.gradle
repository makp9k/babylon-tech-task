// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 21
    ]

    ext.versions = [
            'kotlin': '1.3.31',
            'dagger': '2.23.1'
    ]

    ext.deps = [
            'kotlin_std_lib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
            'androidx'      : [
                    'appcompat'   : 'androidx.appcompat:appcompat:1.1.0-beta01',
                    'recyclerview': 'androidx.recyclerview:recyclerview:1.1.0-alpha06',
                    'material'    : 'com.google.android.material:material:1.1.0-alpha07',
                    'constraint'  : 'androidx.constraintlayout:constraintlayout:2.0.0-beta1',
                    'lifecycle'   : [
                            'viewmodel': "androidx.lifecycle:lifecycle-viewmodel:2.2.0-alpha01"
                    ]
            ],
            'rx'            : [
                    'java'   : 'io.reactivex.rxjava2:rxjava:2.2.9',
                    'kotlin' : 'io.reactivex.rxjava2:rxkotlin:2.3.0',
                    'android': 'io.reactivex.rxjava2:rxandroid:2.1.0',
                    'relay'  : 'com.jakewharton.rxrelay2:rxrelay:2.1.0',
                    'binding': 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2',
            ],
            'tests'         : [
                    'junit'  : 'junit:junit:4.12',
                    'mockito': 'org.mockito:mockito-core:2.28.2',
                    'assertj': 'org.assertj:assertj-core:3.12.2'
            ],
            'timber'        : 'com.github.ajalt:timberkt:1.5.1',
            'dagger'        : [
                    'runtime' : "com.google.dagger:dagger:${versions.dagger}",
                    'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
            ],
            'glide'         : 'com.github.bumptech.glide:glide:4.9.0',
            'gson'          : 'com.google.code.gson:gson:2.8.5',
            'retrofit'      : [
                    'runtime'       : 'com.squareup.retrofit2:retrofit:2.5.0',
                    'gson_converter': 'com.squareup.retrofit2:converter-gson:2.5.0',
                    'rx_adapter'    : 'com.squareup.retrofit2:adapter-rxjava2:2.5.0:'
            ],
            'okhttp'        : [
                    'client': 'com.squareup.okhttp3:okhttp:3.14.2'
            ],
            'koptional'     : [
                    'core'  : 'com.gojuno.koptional:koptional:1.6.0',
                    'rxjava': 'com.gojuno.koptional:koptional-rxjava2-extensions:1.6.0'
            ],
            'project'       : [
                    'common'               : [ path: ':common' ],
                    'commonAndroid'        : [ path: ':common-android' ],
                    'postDetailsScreen'    : [ path: ':post-details-screen' ],
                    'postsCollectionScreen': [ path: ':posts-collection-screen' ],
                    'postsService'         : [
                            'api' : [ path: ':posts-service:api' ],
                            'test': [ path: ':posts-service:test' ],
                            'impl': [ path: ':posts-service:impl' ]
                    ],
                    'rxMvvm'               : [
                            'core'    : [ path: ':rx-mvvm:arch-core' ],
                            'controls': [ path: ':rx-mvvm:arch-controls' ]
                    ],
            ]
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    androidModules(plugins) {
        android {
            compileSdkVersion buildConfig.compileSdk

            defaultConfig {
                minSdkVersion buildConfig.minSdk
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments['dagger.gradle.incremental'] = 'true'
                    }
                }
            }

            dexOptions {
                preDexLibraries true
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            if (project.name != 'app') {
                libraryVariants.all {
                    it.generateBuildConfigProvider.configure {
                        it.enabled = false
                    }
                }
            }
        }
    }

    kaptModules(plugins) {
        kapt {
            useBuildCache = true
        }
    }

    project.afterEvaluate {
        dependencies {
            implementation deps.kotlin_std_lib
            implementation deps.rx.java
            implementation deps.rx.kotlin
            implementation deps.koptional.core
            implementation deps.koptional.rxjava
            implementation deps.timber
        }
    }
}

static def androidModules(plugins, closure) {
    plugins.whenPluginAdded { plugin ->
        if ('com.android.build.gradle.AppPlugin' == plugin.class.name
                || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {
            closure.call()
        }
    }
}

static def kaptModules(plugins, closure) {
    plugins.whenPluginAdded { plugin ->
        if ('kotlin-kapt' == plugin.class.name) {
            closure.call()
        }
    }
}
